apply plugin: "com.android.application"
apply plugin: "kotlin-android"


def secretProperties = new Properties()
secretProperties.setProperty("SIGNING_KEYSTORE_PATH", "${System.getenv("SIGNING_KEYSTORE_PATH")}")
secretProperties.setProperty("SIGNING_KEYSTORE_PASSWORD", "${System.getenv("SIGNING_KEYSTORE_PASSWORD")}")
secretProperties.setProperty("SIGNING_KEY_ALIAS", "${System.getenv("SIGNING_KEY_ALIAS")}")
secretProperties.setProperty("SIGNING_KEY_PASSWORD", "${System.getenv("SIGNING_KEY_PASSWORD")}")

android {
    compileSdkVersion 29
    ndkVersion "21.3.6528147"
    defaultConfig {
        applicationId "agersant.polaris"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "0.0"
    }
    signingConfigs {
        release {
            storeFile file("${secretProperties["SIGNING_KEYSTORE_PATH"]}")
            storePassword "${secretProperties["SIGNING_KEYSTORE_PASSWORD"]}"
            keyAlias "${secretProperties["SIGNING_KEY_ALIAS"]}"
            keyPassword "${secretProperties["SIGNING_KEY_PASSWORD"]}"
        }
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
        }
    }
    buildFeatures {
        viewBinding = true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Core
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation "com.google.android.material:material:1.3.0-beta01"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.2"

    // Preference
    implementation "androidx.preference:preference-ktx:1.1.1"

    // Media notification
    implementation "androidx.media:media:1.2.1"

    // Media player
    implementation "com.google.android.exoplayer:exoplayer-core:2.11.8"
    implementation "com.github.PaulWoitaschek.ExoPlayer-Extensions-Autobuild:flac:2.9.6"

    // Rest client
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "com.squareup.okhttp3:okhttp:4.9.0"

    // Swipe refresh layout
    implementation "com.github.orangegangsters:swipy:1.2.3@aar"
}

task printVersionCode {
    doLast {
        println android.defaultConfig.versionCode
    }
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}
repositories {
    mavenCentral()
}
